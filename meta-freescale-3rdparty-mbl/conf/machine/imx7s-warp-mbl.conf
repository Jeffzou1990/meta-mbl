# Copyright (c) 2018 Arm Limited and Contributors. All rights reserved.
#
# SPDX-License-Identifier: MIT

#@TYPE: Machine
#@NAME: NXP WaRP7 Development Board
#@DESCRIPTION: Machine configuration for the WaRP7 board with extra features enabled

MACHINEOVERRIDES =. "imx7s-warp:${MACHINE}:"
include conf/machine/imx7s-warp.conf
require conf/include/mbl-default.conf

# ------------------------------------------------------------------------------
# non-MBL-specific (upstreamable) configuration
# ------------------------------------------------------------------------------
SERIAL_CONSOLES = "115200;ttymxc0"

MACHINE_EXTRA_RRECOMMENDS += "linux-firmware-bcm43430 bluez-firmware-warp7distro-bcm43430a1-hcd"

# ------------------------------------------------------------------------------
# MBL-specific (non-upstreamable) configuration
# ------------------------------------------------------------------------------


##############################################################################
# KERNEL symbol definitions for virtual/kernel build configuration
#
# KERNEL_CLASSES
#   mbl-fitimage.bbclass is inherited into kernel.bbclass processing by
#   defining this symbol to include mbl-fitimage.
# KERNEL_IMAGETYPE
#   The kernel is packages in a FIT image by specifying "fitImage"
# KERNEL_DEVICETREE
#   Its unnecessary to change this symbol here as the required
#   "imx7s-warp.dtb" value is specified in imx7s-warp.conf (included above)
KERNEL_CLASSES  = "mbl-fitimage"
KERNEL_IMAGETYPE = "fitImage"

##############################################################################
# U-BOOT symbol definitions
#
# UBOOT_SIGN_ENABLE
#   This is used to enable u-boot verified boot. See uboot-sign.bbclass for
#   more information.
##############################################################################
UBOOT_CONFIG = ""
UBOOT_CONFIG[sd] = ""
UBOOT_MACHINE = "warp7_bl33_defconfig"
UBOOT_SUFFIX = "bin"
UBOOT_BINARY = "u-boot.${UBOOT_SUFFIX}"
UBOOT_ENTRYPOINT = "0x80800000"
UBOOT_DTB_LOADADDRESS = "0x83000000"
UBOOT_IMAGE = "mbl-u-boot.bin"
UBOOT_SIGN_ENABLE = "1"

# Specify the ATF recipe to be used for this machine.
PREFERRED_PROVIDER_virtual/atf = "atf-${MACHINE}"

# Don't use any of the firmware-imx-* packages - they're covered by a EULA that
# prevents us doing binary releases
MACHINE_EXTRA_RRECOMMENDS_remove = "firmware-imx-brcm"
MACHINE_EXTRA_RRECOMMENDS_remove = "firmware-imx-vpu-imx6d"
MACHINE_EXTRA_RRECOMMENDS_remove = "firmware-imx-vpu-imx6q"
MACHINE_EXTRA_RRECOMMENDS_remove = "bcm43430-nvram-config"
PACKAGE_EXCLUDE += "firmware-imx-vpu-imx6d"
PACKAGE_EXCLUDE += "firmware-imx-vpu-imx6q"
PACKAGE_EXCLUDE += "firmware-imx-brcm"

# The name of the usbgadget ethernet interface to be used for debugging
MBL_DBG_IFNAME = "usbdbg0"

##############################################################################
# Partition layout config
##############################################################################
MBL_WKS_BOOTLOADER1_FILENAME = "bl2.bin.imx"
MBL_WKS_BOOTLOADER1_OFFSET_BANK1_KiB = "1"
MBL_WKS_BOOTLOADER1_IS_BL2 = "1"

MBL_WKS_BOOTLOADER2_IS_BL3 = "1"

# As reported by "mmc extcsd read /dev/mmcblk1"
MBL_FLASH_ERASE_BLOCK_SIZE_KiB = "6144"

# "blockdev --getsize64 /dev/mmcblk1" reports 7650410496B (= 7296MiB)
MBL_WKS_STORAGE_SIZE_MiB = "7296"

##############################################################################
# SystemD watchdog config
##############################################################################
# Set the max timeout. The imx series watchdog driver explicitly defines a max
# watchdog timeout of 128 seconds.
MBL_WATCHDOG_TIMEOUT_SECS ?= "128"
# Set the device file name for the hardware watchdog
MBL_WATCHDOG_DEVICE_FILENAME ?= "/dev/watchdog"
