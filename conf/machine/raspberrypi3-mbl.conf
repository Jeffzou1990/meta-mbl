# Copyright (c) 2018 Arm Limited and Contributors. All rights reserved.
#
# SPDX-License-Identifier: MIT

#@TYPE: Machine
#@NAME: RaspberryPi 3 Development Board
#@DESCRIPTION: Machine configuration for the RaspberryPi 3 with extra features

MACHINEOVERRIDES =. "raspberrypi3:${MACHINE}:"
include conf/machine/raspberrypi3.conf

# ------------------------------------------------------------------------------
# non-MBL-specific (upstreamable) configuration
# ------------------------------------------------------------------------------


# ------------------------------------------------------------------------------
# MBL-specific (non-upstreamable) configuration
# ------------------------------------------------------------------------------

UBOOT_MACHINE = "rpi_3_32b_signed_config"

KERNEL_CLASSES  = "mbl-fitimage"
KERNEL_IMAGETYPE = "fitImage"
KERNEL_DEVICETREE = "bcm2710-rpi-3-b-plus.dtb"

UBOOT_LOADADDRESS = "0x04000000"
UBOOT_ENTRYPOINT = "0x04000000"
UBOOT_DTB_LOADADDRESS = "0x03000000"
UBOOT_IMAGE = "mbl-u-boot.bin"
UBOOT_SIGN_ENABLE = "1"
RPI_USE_U_BOOT = "1"

WKS_FILE = "mbl-raspberrypi3.wks"

# Tell the meta-raspberrypi layer to send Linux console output to the serial
# console
ENABLE_UART = "1"

# meta-mbl's linux-firmware bbappend (for imx7s-warp-mbl support) conflicts
# with meta-raspberrypi's linux-firmware bbappend (for rpi support). Make sure
# meta-mbl's bbappend doesn't get used for raspberrypi3-mbl
BBMASK += "/meta-mbl/recipes-kernel/linux-firmware/"

# meta-mbl/recipes-bsp/mbl-boot-scr/mbl-boot-scr.bb generates the boot.scr
# and therefore meta-raspberrypi/recipes-bsp/rpi-u-boot-scr/rpi-u-boot-scr.bb
# should not be run. The following will cause an error to be reported if it
# ever runs as part of building an image.  
INHERIT += "blacklist"
PNBLACKLIST[rpi-u-boot-scr] = "Do not use rpi-u-boot-scr.bb as mbl-boot-scr.bb is required for the distribution."

PREFERRED_PROVIDER_virtual/atf = "atf-rpi3"

# A valid u-boot.bin without any packed data is validated by earlier stage
# bootloader. So include u-boot-nodtb.bin here because u-boot.bin is packed
# with u-boot.dtb.

IMAGE_BOOT_FILES = "bcm2835-bootfiles/* \
                 mbl-bootfiles/* \
                 ${@make_dtb_boot_files(d)} \
                 ${@bb.utils.contains('RPI_USE_U_BOOT', '1', \
                    '${KERNEL_IMAGETYPE} u-boot-nodtb.bin;${SDIMG_KERNELIMAGE}', \
                    '${KERNEL_IMAGETYPE};${SDIMG_KERNELIMAGE}', d)} \
                 "
